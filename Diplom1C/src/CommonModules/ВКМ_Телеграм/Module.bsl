  
#Область ПрограммныйИнтерфейс  

// Функция отправляет сообщение в телеграм по токену, указанному в константе
Функция ОтправитьСообщениеВTelegram(ТекстСообщения) Экспорт
	
	ОтправленоУспешно = Ложь;
	
	ТокенЧатБота = Константы.ВКМ_Токен.Получить();
	ИДГруппы = Константы.ВКМ_ИДГруппы.Получить();
	
	Если НЕ ЗначениеЗаполнено(ТокенЧатБота) Тогда	
		ЗаписьЖурналаРегистрации("ВКМ Отправка уведомления сотрудникам",УровеньЖурналаРегистрации.Ошибка, ,,"Не заполнен токен чат бота");	
		Возврат ОтправленоУспешно;
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(ИДГруппы) Тогда	
		ЗаписьЖурналаРегистрации("ВКМ Отправка уведомления сотрудникам",УровеньЖурналаРегистрации.Ошибка, ,,"Не заполнен идентификатор группы");	
		Возврат ОтправленоУспешно;
	КонецЕсли;
	
	Соединение = Новый HTTPСоединение("api.telegram.org", 443, , , , , Новый ЗащищенноеСоединениеOpenSSL());
	
	Данные = новый структура;
	Данные.Вставить("method", "sendMessage");
	Данные.Вставить("chat_id", ИДГруппы);
	Данные.Вставить("text", ТекстСообщения);

	ЗаписьJSON = новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные); 
	
	строказапроса = ЗаписьJSON.Закрыть();  
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("content-type", "application/json");
	
	ТекстЗапроса =  СтрШаблон("bot%1/sendMessage", ТокенЧатБота);
	
	Запрос = Новый HTTPЗапрос(ТекстЗапроса, Заголовки);
	Запрос.УстановитьТелоИзСтроки(строказапроса);
	
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	
	ОтправленоУспешно = Ответ.КодСостояния = 200;
	
	Если НЕ ОтправленоУспешно Тогда
		ЗаписьЖурналаРегистрации("ВКМ Отправка уведомления сотрудникам",УровеньЖурналаРегистрации.Ошибка, ,,"Произошла ошибка пот отправлке смс");	
	КонецЕсли;
	
	Возврат ОтправленоУспешно;
	
КонецФункции

Процедура ВКМ_ОповещенияЧатБот() Экспорт
	
	СписокУдаляемыхЭлементов = Новый Массив;
	
	Выборка = Справочники.ВКМ_Уведомления.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Ссылка, "ТекстСообщения", Истина);
		ОтправленоУспешно = ОтправитьСообщениеВTelegram(ТекстСообщения);
		Если ОтправленоУспешно Тогда
			СписокУдаляемыхЭлементов.Добавить(Выборка.Ссылка);	
		КонецЕсли;	
	КонецЦикла;   

	Для Каждого Стр Из СписокУдаляемыхЭлементов Цикл
		Стр.ПолучитьОбъект().Удалить();	
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти