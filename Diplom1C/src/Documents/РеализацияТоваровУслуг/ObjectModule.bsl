
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++ Гудилин А. В.
Процедура ВКМ_ВыполнитьАвтозаполнение() Экспорт 

	НоменклатураАбонентскаяПлата = Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить();
	НоменклатураРаботаСпециалиста = Константы.ВКМ_НоменклатураРаботаСпециалиста.Получить();
	
	Если НоменклатураАбонентскаяПлата = Справочники.Номенклатура.ПустаяСсылка() Тогда
		ОбщегоНазначения.СообщитьПользователю("Не заполнена константа ""Номенклатура абонентская плата""");
		Возврат;
	КонецЕсли;
	
	Если НоменклатураРаботаСпециалиста = Справочники.Номенклатура.ПустаяСсылка() Тогда
		ОбщегоНазначения.СообщитьПользователю("Не заполнена константа ""Номенклатура работы специалиста""");
		Возврат;
	КонецЕсли;
	
	Услуги.Очистить();
	
	ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "ВКМ_СуммаЕжемесячнойАбоненскойПлаты, ВКМ_СтоимостьЧасаРаботы");
	
	Если ДанныеДоговора.ВКМ_СуммаЕжемесячнойАбоненскойПлаты <> 0 Тогда
		СтрУслуга = Услуги.Добавить();
		СтрУслуга.Номенклатура = НоменклатураАбонентскаяПлата;
		СтрУслуга.Количество = 1;
		СтрУслуга.Цена = ДанныеДоговора.ВКМ_СуммаЕжемесячнойАбоненскойПлаты;
		СтрУслуга.Сумма = ДанныеДоговора.ВКМ_СуммаЕжемесячнойАбоненскойПлаты
	КонецЕсли;
	
	ДанныеОВыполненныхРаботах = ВКМ_ПолучитьВыполненныеРаботыЗаМесяц(Дата, Контрагент, Договор);
	
	Если ДанныеОВыполненныхРаботах.Сумма <> 0 Тогда
		СтрУслуга = Услуги.Добавить();
		СтрУслуга.Номенклатура = НоменклатураРаботаСпециалиста;
		СтрУслуга.Количество = ДанныеОВыполненныхРаботах.КоличествоЧасов;
		СтрУслуга.Цена = ДанныеДоговора.ВКМ_СтоимостьЧасаРаботы;
		СтрУслуга.Сумма = ДанныеОВыполненныхРаботах.Сумма;
	КонецЕсли;
	
	СуммаДокумента = Услуги.Итог("Сумма");
	
КонецПроцедуры //--- Гудилин А. В.

//+++ Гудилин А. В.
Функция ВКМ_ПолучитьВыполненныеРаботыЗаМесяц(МесяцПолучения, КлиентПоиска, ДоговорПоиска)

	Результат = Новый Структура;
	Результат.Вставить("КоличествоЧасов", 0);
	Результат.Вставить("Сумма", 0);
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ВКМ_ЗадолженностиКлиентовОбороты.ВКМ_КоличествоЧасовОборот) КАК ВКМ_КоличествоЧасовОборот,
		|	СУММА(ВКМ_ЗадолженностиКлиентовОбороты.ВКМ_СуммаОборот) КАК ВКМ_СуммаОборот
		|ИЗ
		|	РегистрНакопления.ВКМ_ЗадолженностиКлиентов.Обороты(
		|			&НачалоМесяца,
		|			&КонецМесяца,
		|			,
		|			ВКМ_Клиент = &Клиент
		|				И ВКМ_Договор = &Договор) КАК ВКМ_ЗадолженностиКлиентовОбороты";
	
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(МесяцПолучения));
	Запрос.УстановитьПараметр("КонецМесяца",  КонецМесяца(МесяцПолучения));
	
	Запрос.УстановитьПараметр("Клиент",   КлиентПоиска);
	Запрос.УстановитьПараметр("Договор",  ДоговорПоиска);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Результат.КоличествоЧасов = Выборка.ВКМ_КоличествоЧасовОборот;
		Результат.Сумма = Выборка.ВКМ_СуммаОборот;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции //--- Гудилин А. В.

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.Договор КАК Договор,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Товары,
	               |	ЗаказПокупателя.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;
	
	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;
	
	Основание = ДанныеЗаполнения;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	Движение = Движения.ОбработкаЗаказов.Добавить();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Заказ = Основание;
	Движение.СуммаОтгрузки = СуммаДокумента;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
