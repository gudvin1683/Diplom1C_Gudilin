
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дата = ТекущаяДатаСеанса(); 
		
	СоздатьИзмеренияПланировка();
	
	ОбновитьПериодОтображенияПланировщика();
	
	ЗаполнитьЖурналЗаписей();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Изменение_ОбслуживаниеКлиента" Тогда
		ЗаполнитьЖурналЗаписей();	
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ВКМ_ДатаПроведенияРабот", НачалоДня(Начало));
	ЗначенияЗаполнения.Вставить("ВКМ_ВремяНачалаРаботПлан", Начало - (НачалоДня(Начало) - Дата(1,1,1)));
	
	ЗначенияЗаполнения.Вставить("ВКМ_Специалист", ЗначенияИзмерений["Сотрудник"]);
	
	СтруктураПараметров = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиента.Форма.ВКМ_ФормаДокумента", СтруктураПараметров); 

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	
	ЭлементПланировщика = ВыделенныеЭлементы[0];
	
	СтруктураПараметров = Новый Структура("Ключ", ЭлементПланировщика.Значение);
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиента.Форма.ВКМ_ФормаДокумента", СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	ЭлементПланировщика = ВыделенныеЭлементы[0];
	
	ЗначенияРеквизитов = Новый Структура;
	ЗначенияРеквизитов.Вставить("Дата", ЭлементПланировщика.Начало);
	ЗначенияРеквизитов.Вставить("ВКМ_ДатаПроведенияРабот", НачалоДня(ЭлементПланировщика.Начало));
	ЗначенияРеквизитов.Вставить("ВКМ_ВремяНачалаРаботПлан", ЭлементПланировщика.Начало - (НачалоДня(ЭлементПланировщика.Начало) - Дата(1,1,1)) );
	
	ЗначенияРеквизитов.Вставить("ВКМ_Специалист", ЭлементПланировщика.ЗначенияИзмерений["Сотрудник"]);

	ОбновитьДанныеЗаявки(ЭлементПланировщика.Значение, ЗначенияРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	   ОбновитьПериодОтображенияПланировщика();
	   ЗаполнитьЖурналЗаписей();
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ОбновитьПериодОтображенияПланировщика()
	
	Начало = НачалоДня(Дата) + 9*3600;
	Конец = НачалоДня(Дата) + 19*3600;
	
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(Начало, Конец);
	

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЖурналЗаписей()
	
	Планировщик.Элементы.Очистить();
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.ВКМ_Клиент КАК Клиент,
		|	ВКМ_ОбслуживаниеКлиента.ВКМ_Договор КАК Автомобиль,
		|	ВКМ_ОбслуживаниеКлиента.ВКМ_ДатаПроведенияРабот КАК ДатаПроведенияРабот,
		|	ВКМ_ОбслуживаниеКлиента.ВКМ_Специалист КАК Сотрудник,
		|	ВКМ_ОбслуживаниеКлиента.ВКМ_ВремяНачалаРаботПлан КАК ВремяНачалаРаботПлан,
		|	ВКМ_ОбслуживаниеКлиента.ВКМ_ВремяОкочнанияРаботПлан КАК ВремяОкочнанияРаботПлан,
		|	ВКМ_ОбслуживаниеКлиента.ВКМ_ОписаниеПроблемы КАК ОписаниеПроблемы, 
		|	ВКМ_ОбслуживаниеКлиента.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.ВКМ_ДатаПроведенияРабот МЕЖДУ &НП И &КП
		|	И ВКМ_ОбслуживаниеКлиента.Проведен";
	
	Запрос.УстановитьПараметр("КП", КонецДня(Дата));
	Запрос.УстановитьПараметр("НП", НачалоДня(Дата));
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЗначенияИзмерений = Новый Соответствие;
		ЗначенияИзмерений.Вставить("Сотрудник", Выборка.Сотрудник);
		
		МассивСтрок = Новый Массив;
		
		ПредставлениеКлиента = Строка(Выборка.Клиент);
		
		ЖирныйШрифт = Новый Шрифт(,,Истина);
				
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ПредставлениеКлиента, ЖирныйШрифт));
		МассивСтрок.Добавить(Символы.ПС);
		
		Если ЗначениеЗаполнено(Выборка.ОписаниеПроблемы) Тогда
			МассивСтрок.Добавить("--" + Выборка.ОписаниеПроблемы);	
		КонецЕсли;
		
		НачалоВыполненияРабот = Выборка.ДатаПроведенияРабот + (Выборка.ВремяНачалаРаботПлан - Дата(1,1,1));
		ОкончаниеВыполненияРабот = Выборка.ДатаПроведенияРабот + (Выборка.ВремяОкочнанияРаботПлан - Дата(1,1,1));
				
		ЭлементПланировщика = Планировщик.Элементы.Добавить(НачалоВыполненияРабот, ОкончаниеВыполненияРабот);
		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(ЗначенияИзмерений);
		ЭлементПланировщика.Значение = Выборка.Ссылка;
		ЭлементПланировщика.Текст = Новый ФорматированнаяСтрока(МассивСтрок); 
		
	КонецЦикла;
		
КонецПроцедуры // ()

&НаСервере
Процедура СоздатьИзмеренияПланировка()
	
	Измерение = Планировщик.Измерения.Добавить("Сотрудник");
	
	Выборка =  Справочники.Пользователи.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ПометкаУдаления ИЛИ Выборка.Недействителен ИЛИ Выборка.Служебный Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеИзмерения = Измерение.Элементы.Добавить(Выборка.Ссылка);
		ЗначениеИзмерения.Текст = Выборка.Наименование + Символы.ПС + Выборка.Подразделение;
		ЗначениеИзмерения.ЦветРамки = WebЦвета.Оранжевый;
		
	КонецЦикла; 
	
КонецПроцедуры // ()

&НаСервереБезКонтекста
Процедура ОбновитьДанныеЗаявки(ВКМ_ОбслуживаниеКлиента, ЗначенияРеквизитов)
	
	  ОбъектЗаписи = ВКМ_ОбслуживаниеКлиента.ПолучитьОбъект();
	  
	  ЕстьИзмения = Ложь;
	  Для каждого Реквизит Из ЗначенияРеквизитов Цикл
	      Если ОбъектЗаписи[Реквизит.Ключ] <> Реквизит.Значение Тогда
		       ЕстьИзмения = Истина;
		  	   Прервать;		  
		  КонецЕсли;
	  КонецЦикла;
	  
	  Если ЕстьИзмения Тогда
		  ЗаполнитьЗначенияСвойств(ОбъектЗаписи, ЗначенияРеквизитов);
		  ОбъектЗаписи.Записать(РежимЗаписиДокумента.Проведение);
	  КонецЕсли;

КонецПроцедуры // ()
	
#КонецОбласти
	

